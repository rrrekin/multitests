/*
 * Apache License, Version 2.0
 *
 * You may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'java'

repositories {
    mavenCentral()
}

def testedVersion = rootProject.version
//def testedVersion = '0.7.0'
def testedJdk = System.getProperty('java.version')
def jdkVendor = System.getProperty('java.vm.vendor')

def junitTestedVersion = project.hasProperty('junit') ? junit : '4.+'

def reportsDirectory = "reports" as File

dependencies {
    testCompile "junit:junit:$junitTestedVersion"
    testCompile "com.github.rrrekin:multitests-junit4:${testedVersion}"
}

build.dependsOn('copyTestResults')

test {
    ignoreFailures = true
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

task buildTestReport {
    doLast {
        reportsDirectory.mkdirs()
        def destFile = new File(reportsDirectory, "test_report_${testedVersion}_JDK${testedJdk}_junit-${junitTestedVersion}.md")
        println "Generating report: $destFile"
        destFile.text = "# Compatibility test results for multitests library v. ${testedVersion} using JUnit v. ${junitTestedVersion} and Java ${testedJdk} from ${jdkVendor}\n"

        def files = fileTree("$buildDir/test-results").include('*.xml')

        def compatible = true
        def resultsFound = false
        files.each { srcFile ->
            def testResults = new XmlSlurper().parse(srcFile)
            testResults.testcase.each { testcase ->
                def name = testcase.@name
                def className = (testcase.@classname as String).replaceFirst(/.*\./, '')
                def skipped = testcase.skipped.size() != 0
                def failed = testcase.failure.size() != 0
                def message = '**OK**'
                if (failed) {
                    compatible = false
                    message = "**FAILED**: ${testcase.failure.@message}"
                } else if (skipped) {
                    message = '~~skipped~~'
                }
                destFile << "- $className.$name -> $message\n"
                resultsFound = true
            }
        }
        if (!resultsFound) {
            destFile << "\nUnable to execute tests\n"
            compatible = false
        }
        destFile << "\n\nCompatible: ${compatible ? 'YES' : 'NO'}\n"
        
        // Generate index
        File[] reports = fileTree(reportsDirectory).include('test_report_*.md').files.sort{ a,b ->
            a.name.replaceAll(/\d+/){"00000$it".reverse().take(6).reverse()} <=> b.name.replaceAll(/\d+/){"00000$it".reverse().take(6).reverse()} }
        def indexFile = new File(reportsDirectory, "index.md")
        indexFile.text = "# Compatibility testing report\n"
        def lastVersion = '-1'
        reports.each { report ->
            def libVersion = (report.name =~ /^test_report_([^_]*)/)[0][1]
            if(libVersion != lastVersion) {
                lastVersion = libVersion
                indexFile << "\n## Multitests ver. $libVersion\n\n"
            }
            def result = (report.text =~ /(?ms)^Compatible:\s*(.*)\s*$/)[0][1]
            def mark = result.toString().toUpperCase().trim()=='YES'?'':'~~'
            indexFile << "* [$mark${report.name.replaceFirst('\\.md','')}$mark Compatible: $result](${report.name})\n"
        }
    }
}

buildTestReport.mustRunAfter(test)
